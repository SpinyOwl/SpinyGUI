plugins {
    id "org.sonarqube" version "2.8"
    id "org.javamodularity.moduleplugin" version "1.7.0" apply false
    id "checkstyle"
}

project.group = 'com.spinyowl'

printProjectInfo()

private void printProjectInfo() {
    println "##################################################"
    println "# Project info:                                  #"
    println sprintf("#     group:    %-33s#", project.group)
    println sprintf("#     name:     %-33s#", project.name)
    println sprintf("#     version:  %-33s#", project.version)
    println "##################################################"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.javamodularity.moduleplugin'

    group = 'com.spinyowl'
    version = rootProject.version

    buildDir = new File(rootProject.projectDir, "build/" + project.name)

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    test {
        useJUnitPlatform()

        testLogging {
            events 'PASSED', 'FAILED', 'SKIPPED'
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'

        testCompileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    }
    compileJava {
        moduleOptions {
            addModules = ['org.lwjgl.natives']
        }
    }
}

def sonarLogin = System.getProperty("sonarLogin") ?: System.getenv("sonarLogin") ?: null

sonarqube {
    properties {
        property "sonar.projectKey", "SpinyOwl_SpinyGUI"
        property "sonar.organization", "spinyowl"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", sonarLogin
    }
}