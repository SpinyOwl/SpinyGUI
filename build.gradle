plugins {
    id "org.sonarqube" version "2.7"
}

project.group = 'com.spinyowl'

println "##################################################"
println "# Project info:                                  #"
println sprintf("#     group:    %-33s#", project.group)
println sprintf("#     name:     %-33s#", project.name)
println sprintf("#     version:  %-33s#", project.version)
println "##################################################"

subprojects {

    buildDir = new File(rootProject.projectDir, "build/" + project.name)

    afterEvaluate {
        group = 'com.spinyowl'
        version = rootProject.version

        repositories {
            mavenCentral()
            jcenter()
            maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
            maven { url "https://raw.githubusercontent.com/LiquidEngine/repo/snapshots" }
            maven { url "https://raw.githubusercontent.com/LiquidEngine/repo/releases" }

        }

        sourceCompatibility = 1.12
        targetCompatibility = 1.12

        compileJava {
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                ]
                classpath = files()
            }
        }

        compileTestJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'junit',
                        '--add-reads', "$moduleName=junit",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                ]
                classpath = files()
            }
        }

        test {
            inputs.property("moduleName", moduleName)
            doFirst {
                jvmArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'ALL-MODULE-PATH',
                        '--add-reads', "$moduleName=junit",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
                ]
                classpath = files()
            }
        }
    }
}

def sonarLogin = System.getProperty("sonarLogin") ?: System.getenv("sonarLogin") ?: null

sonarqube {
    properties {
        property "sonar.projectKey", "SpinyOwl_SpinyGUI"
        property "sonar.organization", "spinyowl"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", sonarLogin
    }
}