plugins {
    id "org.sonarqube" version "3.1.1"
    id "checkstyle"
}

project.group = 'com.spinyowl'

printProjectInfo()

private void printProjectInfo() {
    println "##################################################"
    println "# Project info:                                  #"
    println sprintf("#     group:    %-33s#", project.group)
    println sprintf("#     name:     %-33s#", project.name)
    println sprintf("#     version:  %-33s#", project.version)
    println "##################################################"
}

subprojects {
    group = 'com.spinyowl'
    version = rootProject.version

    buildDir = new File(rootProject.projectDir, "build/" + project.name)

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    plugins.withType(JavaPlugin).configureEach {
        java {
            modularity.inferModulePath = true
        }

        sourceCompatibility = 1.15
        targetCompatibility = 1.15

        dependencies {

            compileOnly group: 'org.projectlombok', name: 'lombok', version: lombok_version
            annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombok_version

            testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombok_version
            testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombok_version

            // api
            // implementation
            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
            testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}

def sonarLogin = System.getProperty("sonarLogin") ?: System.getenv("sonarLogin") ?: null

sonarqube {
    properties {
        property "sonar.projectKey", "SpinyOwl_SpinyGUI"
        property "sonar.organization", "spinyowl"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", sonarLogin
    }
}